buildscript {
	ext {
		springBootVersion = '2.0.5.RELEASE'
	}
	repositories {
		mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:1.2.0")
	}
}

apply plugin: "com.moowork.node"
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'pers.henglin'
version = '1.0'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	implementation('org.springframework.boot:spring-boot-starter-data-rest')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2')
	implementation('org.codehaus.groovy:groovy-all:2.4.7')

	runtimeOnly('com.h2database:h2')

	testImplementation('org.springframework.boot:spring-boot-starter-test')
}

node {
	version = '9.3.0'
	download = true
	nodeModulesDir = file("${project.projectDir}/frontend")
}

task webpack(type: YarnTask, dependsOn: yarn) {
	args = ['build']
}

task moveJStoSpring(type: Copy) {
	from "${project.projectDir}/frontend/build/"
	into "${project.projectDir}/src/main/resources/static/"
}

moveJStoSpring.dependsOn 'webpack'
processResources.dependsOn 'moveJStoSpring'